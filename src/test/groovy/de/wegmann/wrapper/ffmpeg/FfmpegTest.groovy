package de.wegmann.wrapper.ffmpeg

import groovy.time.Duration

import java.security.MessageDigest

/**
 * User: andy
 * Date: 21.06.13
 */
class FfmpegTest extends GroovyTestCase {
    /**
     * copy test file to temp dir
     */
    void setUp() {
        def antBuilder = new AntBuilder()
        antBuilder.mkdir(dir: "src/test/resources/temp")
        antBuilder.copy(file: "src/test/resources/Audiobook_Test_plain.m4a",
                tofile: "src/test/resources/temp/Audiobook_Test.m4a")
    }

    /**
     * remove temp directory to clean up
     */
    void tearDown() {
        def antBuilder = new AntBuilder()
        antBuilder.delete(dir: "src/test/resources/temp")
    }

    /**
     * Test convertion to m4a file. Test data is generated by converting an m4a to flac and back again.
     */
    void testConvertAudioM4a() {
        def audioM4a = new Ffmpeg("src/test/resources/temp/Audiobook_Test.m4a")
        audioM4a.convertToFlac("src/test/resources/temp/Audiobook_Test.flac")

        assertTrue("flac file existiert nicht", new File("src/test/resources/temp/Audiobook_Test.flac").exists())

        def audioFlac = new Ffmpeg("src/test/resources/temp/Audiobook_Test.flac")
        audioFlac.convertToM4a("src/test/resources/temp/Audiobook_Test2.m4a")

        assertTrue("m4a file existiert nicht", new File("src/test/resources/temp/Audiobook_Test2.m4a").exists())
    }

    /**
     * Test if convert to m4a and m4b result in identical files.
     */
    void testConvertAudioM4b() {
        def audioM4a = new Ffmpeg("src/test/resources/temp/Audiobook_Test.m4a")
        audioM4a.convertToFlac("src/test/resources/temp/Audiobook_Test.flac")

        assertTrue("flac file existiert nicht", new File("src/test/resources/temp/Audiobook_Test.flac").exists())

        def audioFlac = new Ffmpeg("src/test/resources/temp/Audiobook_Test.flac")
        audioFlac.convertToM4b("src/test/resources/temp/Audiobook_Test2.m4b")
        audioFlac.convertToM4a("src/test/resources/temp/Audiobook_Test2.m4a")

        def m4afile = new File("src/test/resources/temp/Audiobook_Test2.m4a")
        assertTrue("m4a file existiert nicht", m4afile.exists())
        def m4bfile = new File("src/test/resources/temp/Audiobook_Test2.m4b")
        assertTrue("m4b file existiert nicht", m4bfile.exists())
        assertEquals("md5summen sind nicht gleich", md5sum(m4afile), md5sum(m4bfile))
    }

    /**
     * create an md5 sum for a given file
     * @param file file to generate md5sum for
     * @return string with md5sum
     */
    private String md5sum(File file) {
        assert file != null
        def md = MessageDigest.getInstance("MD5")
        md.update(file.readBytes())
        new BigInteger(1, md.digest()).toString(16)
    }

    void testGetDuration() {
        try {
            def nonExistingFile = new Ffmpeg("not/existing")
            assertNotNull(nonExistingFile)
            nonExistingFile.getDuration()
            fail("should throw IllegalArgumentException")
        } catch (IllegalArgumentException e) {
            assertTrue(e.message.contains("file not/existing does not exists"))
        }

        def audiofile = new Ffmpeg("src/test/resources/temp/Audiobook_Test.m4a")
        assertEquals("got wrong duration from file", new Duration(0,0,0,21,60), audiofile.getDuration())
    }
}
